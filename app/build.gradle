apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

repositories {
  maven { url 'https://jitpack.io' }
}

apply from: 'jacoco.gradle'

android {
  compileSdkVersion 28
  defaultConfig {
    applicationId 'app.envelop'
    minSdkVersion 21
    targetSdkVersion 28
    versionCode 16
    versionName "1.2.1"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }
  buildTypes {
    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
    debug {
      applicationIdSuffix '.debug'
      testCoverageEnabled true
    }
  }
  testOptions {
    animationsDisabled = true
  }
  lintOptions {
    checkAllWarnings true
    warningsAsErrors true
    abortOnError true
    disable 'CheckResult'
  }
  androidExtensions {
    experimental = true
  }
  kapt {
    correctErrorTypes = true
    arguments {
      arg("room.schemaLocation", "$projectDir/schemas".toString())
    }
  }
  packagingOptions {
    exclude 'META-INF/*'
  }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0-M2"
  implementation 'androidx.appcompat:appcompat:1.0.2'
  implementation 'androidx.core:core-ktx:1.0.2'
  implementation 'com.google.android.material:material:1.1.0-alpha07'

  // Architecture Components
  implementation 'androidx.lifecycle:lifecycle-viewmodel:2.0.0'
  implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
  kapt 'androidx.lifecycle:lifecycle-compiler:2.0.0'

  // Logging
  implementation 'com.jakewharton.timber:timber:4.7.1'

  // Serialization
  implementation 'com.google.code.gson:gson:2.8.5'

  // Blockstack
  // implementation 'com.github.blockstack:blockstack-android:0.4.6'
  // Fork with temporary fix for proper network errors
  implementation 'com.github.blockstack:blockstack-android:96fb40f'

  // Dependency Injection
  implementation 'com.google.dagger:dagger:2.23'
  kapt 'com.google.dagger:dagger-compiler:2.23'

  // Database ORM
  def room_version = '2.1.0'
  implementation "androidx.room:room-runtime:$room_version"
  implementation "androidx.room:room-rxjava2:$room_version"
  kapt "androidx.room:room-compiler:$room_version"

  // Rx
  implementation 'io.reactivex.rxjava2:rxjava:2.2.8'
  implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'
  implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
  implementation 'com.trello.rxlifecycle3:rxlifecycle-android-lifecycle-kotlin:3.0.0'
  implementation 'com.f2prateek.rx.preferences2:rx-preferences:2.0.0'
  def rxbinding_version = '3.0.0-alpha2'
  implementation "com.jakewharton.rxbinding3:rxbinding-core:$rxbinding_version"
  implementation "com.jakewharton.rxbinding3:rxbinding-appcompat:$rxbinding_version"
  implementation "com.jakewharton.rxbinding3:rxbinding-material:$rxbinding_version"
  implementation "com.jakewharton.rxbinding3:rxbinding-swiperefreshlayout:$rxbinding_version"

  // Lists
  implementation 'com.airbnb.android:epoxy:3.5.1'
  kapt 'com.airbnb.android:epoxy-processor:3.5.1'

  // Encryption
  implementation 'com.madgag.spongycastle:bcpkix-jdk15on:1.58.0.0'
  implementation 'com.madgag.spongycastle:bcpg-jdk15on:1.58.0.0'

  // Testing
  testImplementation 'junit:junit:4.12'
  testImplementation 'io.mockk:mockk:1.9.3'
  androidTestImplementation 'androidx.test:runner:1.2.0'
  androidTestImplementation 'androidx.test:rules:1.2.0'
  androidTestImplementation 'androidx.test.ext:junit:1.1.1'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
  androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
}
